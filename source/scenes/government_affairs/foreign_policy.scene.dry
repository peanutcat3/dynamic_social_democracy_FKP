title: Foreign Policy
new-page: true
is-card: true
tags: govt_affairs
on-arrival: month_actions += 1; foreign_policy_timer += 8
view-if: spd_in_government = 1 and foreign_minister_party = "SPD" and  foreign_policy_timer <= 0
card-image: img/portraits/BreitscheidRudolf.jpg
go-to: fp_menu
#subtitle: International relations and dealing with Versailles.

= Foreign Policy

What should our foreign policy priority be?

@fp_menu

- @improve_west
- @improve_east
- @improve_soviet
- @customs_union
- @negotiate
- @reduce_reparations
- @expose
- @european_union
- @european_union_2
- @concordat_negotiate
- @concordat
- @root: Do not enact any policies for now.
- @easy_discard
- @cancel_advisor_action

@improve_west
title: Improve our relations with the <span style="color: #002654;">Western</span> <span style="color: #D7141A;">Allies</span>.
on-arrival: west_relation += 2; reparations_negotiation += 1

The key to peace in Europe is a rapprochement between <span style="color: #002654;">France</span> and Germany. We are taking steps towards that rapprochement. [? if young_plan_seen == 0 : This is difficult to do when <span style="color: #002654;">France</span> is occupying a major portion of Germany's territory, but we are working on that part. ?]

@improve_east
title: Improve our relations with our eastern neighbors.
on-arrival: east_relation += 2;

We also aim to improve relations with our eastern neighbors, <span style="color: #DC143C;">Pol</span><span style="color: #FFFFFF;">and</span> and <span style="color: #11457E;">Czech</span><span style="color: #FFFFFF;">oslo</span><span style="color: #D7141A;">vakia</span>, who are rightfully terrified of German aggression. Hopefully we can convince them of our peaceful intentions.

@improve_soviet
title: Improve our relations with the <span style="color: #750e0e;">**Soviet Union**</span>.
subtitle: We could also broach the question of the <span style="color: #700000;">**KPD**</span>...

The <span style="color: #750e0e;">Soviet Union</span> also suffered from the international order created by the Treaty of Versailles; they have been participating in our secret rearmament program. The Treaty of Rapallo in 1922 affirmed our friendly relations.

The <span style="color: #940000;">Soviets</span> are asking for economic and developmental assistance. They are still a poor country on the whole, and are desperately trying to catch up to the western powers. There is a chance that our <span style="color: #FF0000;">anti</span>-<span style="color: #700000;">communist</span> coalition partners would be upset, but we could assuage them by asking the <span style="color: #940000;">Soviets</span> for secret assistance in training and equipping the Reichswehr.

- @soviet_economic_aid_yes_mil: Provide aid, in exchange for Reichswehr assistance.
- @soviet_economic_aid_yes: Provide aid, in exchange for only good relations.
- @soviet_economic_aid_no: Don't provide aid.

@soviet_economic_aid_yes_mil
choose-if: budget >= 1
subtitle: -1 budget
on-arrival: budget -= 1; soviet_relation += 1; reichswehr_strength += 20; reichswehr_loyalty += 0.05; foreign_goal_completed += 1

The <span style="color: #940000;">Soviets</span> are pleased that we have sent them aid, and have agreed to help train our military. The Reichswehr appreciates our support.

@soviet_economic_aid_yes
choose-if: budget >= 1
subtitle: -1 budget
on-arrival: budget -= 1; soviet_relation += 2; coalition_dissent += 1; foreign_goal_completed += 1

The <span style="color: #940000;">Soviets</span> are pleased that we have sent them aid, and have not asked for anything specific in exchange. Our coalition partners are less pleased.

- @kpd: Ask if <span style="color: #940000;">Moscow</span> could help repair our relations with the <span style="color: #700000;">**KPD**</span>.
- @wittorf: Do the <span style="color: #940000;">Soviets</span> know about the Wittorf Affair? Do they know that we know?
- @root: Do not mention the <span style="color: #700000;">**KPD**</span>.

@soviet_economic_aid_no

The <span style="color: #940000;">Soviets</span> are slightly upset, but they seemed to expect this answer.


@kpd
on-arrival: kpd_relation += 8*(1-dissent); communist_coalition += 1 if kpd_foreign_seen == 0; kpd_foreign_seen = 1

We also mention the <span style="color: #700000;">**KPD**</span> in our conversations with the <span style="color: #940000;">Soviet</span> ambassador. They claim that the <span style="color: #700000;">**KPD**</span> is entirely independent of the <span style="color: #940000;">Soviet</span> government, and that the conflict is Thälmann's doing. But, soon after our conversation we notice a slight improvement in attitudes from the <span style="color: #700000;">**KPD**</span>.

@wittorf
view-if: wittorf_secret = 1 and ((year = 1929 and month < 6) or year = 1928) and wittorf_soviet_union = 0
unavailable-subtitle: It is impossible that <span style="color: #8B0000;">Stalin</span> and the <span style="color: #D50000;">Comintern</span> could be moved on their policy.
on-arrival: wittorf_soviet_union = 1; kpd_relation += 6*(1-dissent); kpd_foreign_seen = 1
max-visits: 1

Of course <span style="color: #8B0000;">Stalin</span> and the various leaders of the <span style="color: #D50000;">Comintern</span> know about the Wittorf Affair, but they do not seem to know that we know as well. The <span style="color: #D50000;">Comintern</span> was planning to build a narrative around the affair that absolves Thälmann, but perhaps now they will be less inclined to defend him.


@customs_union
title: Form a customs union with <span style="color: #EF3340;">Au</span><span style="color: #ffffff;">str</span><span style="color: #EF3340;">ia</span>?
subtitle: This will likely incur the wrath of <span style="color: #002654;">France</span>.
unavailable-subtitle: This is superseded by the <span style="color: #001489;">European</span> <span style="color: #FFDD00;">Union</span>.
choose-if: eu = 0
view-if: black_thursday_seen = 1 and customs_union = 0

The right has long desired closer ties with <span style="color: #EF3340;">Au</span><span style="color: #ffffff;">str</span><span style="color: #EF3340;">ia</span>, with the eventual goal of the merger of the two German-speaking countries. A customs union with <span style="color: #EF3340;">Au</span><span style="color: #ffffff;">str</span><span style="color: #EF3340;">ia</span> is the first step towards this goal. Additionally, many believe that a union with <span style="color: #EF3340;">Au</span><span style="color: #ffffff;">str</span><span style="color: #EF3340;">ia</span> would improve our current economic circumstances.

However, the Allies, especially <span style="color: #002654;">France</span>, are likely to object. This agreement would break the terms of the Versailles Treaty, and could lead to economic sanctions, further hurting our economy.

- @customs_union_2: Yes, do it!
- @fp_menu: No, we should do something else.

@customs_union_2
on-arrival: banking_crisis_timer = 2; customs_union = 1; west_relation -= 2; east_relation -= 2; z_relation += 6; dvp_relation += 6; ddp_relation += 4 if (ddp_ideology == "Right" or dstp_formed == 1); coalition_dissent -= 1 if coalition_dissent > 0; coup_progress -= 1
max-visits: 1

We have formed a customs union with <span style="color: #EF3340;">Au</span><span style="color: #ffffff;">str</span><span style="color: #EF3340;">ia</span>! The right-wing parties are praising us for this unexpected turn of events.

Immediately, <span style="color: #002654;">France</span> and other neighboring countries lodge their protests. The longer-term response is yet to be seen.


@negotiate
title: Negotiate regarding our remaining obligations from the Treaty of Versailles.
subtitle: [? if young_plan_seen == 1 and black_thursday_seen : After passing the Young Plan, <span style="color: #002654;">France</span> has committed to withdrawing from the Rhineland. Now, we must see if they can reduce our reparations further, given the economic depression. ?][? if young_plan_seen == 1 and not black_thursday_seen : After passing the Young Plan, <span style="color: #002654;">France</span> has committed to withdrawing from the Rhineland. Now, we must see if they can reduce our reparations further. ?]
view-if: reparations > -2 and reparations_negotiation < 3
on-arrival: west_relation += 1; reparations_negotiation += 1; reparations_negotiation += 1 if pacifism >= 3; reparations_negotiations += 1 if nationalism <= 50; reparations_negotiation += 1 if west_relation >= 3

Negotiations proceed smoothly for the most part. The Western powers are convinced of Germany's sincerity and goodwill. [? if nationalism <= 50 or pacifism >= 3 : They are pleased to see that we are renouncing militarism. ?]


@reduce_reparations
title: The <span style="color: #0A3161;">Western</span> <span style="color: #C8102E;">Allies</span> are finally willing to pause our reparations payments!
subtitle: Our negotiations have been successful.
view-if: reparations_negotiation >= 3 and black_thursday_seen == 1 and reparations > -2
on-arrival: west_relation += 1; reparations = -2; budget += 2 if hoover_memorandum_seen == 0; budget += 1 if hoover_memorandum_seen == 1; coalition_dissent -= 1 if coalition_dissent > 0; workers_spd += 8*(1-dissent); old_middle_spd += 5*(1-dissent); new_middle_spd += 5*(1-dissent); rural_spd += 5*(1-dissent); coup_progress -= 2 if coup_progress > 2; dvp_left += 3; pro_republic += 5; ddp_left += 1; ddp_cohesion += 1
max-visits: 1

Finally, given both the economic circumstances and our continued negotiations, the <span style="color: #0A3161;">Western</span> <span style="color: #C8102E;">Allies</span> are willing to pause our reparations payments! Much of the credit must go to <span style="color: #0A3161;">Western</span> president Hoover, who has been friendlier towards us than the <span style="color: #001489;">Europeans</span>.

This has reduced tensions within our government, and our party has gotten credit for the achievement.

@expose
title: Expose the secret rearmament plans of the previous foreign ministry!
view-if: rearmament_exposed == 0
on-arrival: west_relation += 2; east_relation += 1; nationalism -= 5*(1-dissent); pro_republic += 5*(1-dissent); coalition_dissent += 1; rearmament_exposed += 1; reparations_negotiation += 3; coup_progress += 2; dvp_right += 2; ddp_left += 1; ddp_cohesion -= 1

The previous foreign ministry, despite its benevolent and peaceful face, was secretly helping the Reichswehr to build up its forces, in contravention of the Treaty of Versailles.

The Allies are grateful of our exposure and repudiation of militarism. Our partners in the coalition are less supportive.

# TODO, add something like schliecher or something idk he gets mad at this behavior

@economic_conference
title: Organize an international economic conference to deal with the Depression.
unavailable-subtitle: [? if west_relation < 4 : Our relations with the West are not good enough. ?]
view-if: black_friday_seen == 1 and crisis_urgency >= 1 and reparations <= -2
choose-if: west_relation >= 4
on-arrival: economic_conference = 1; west_relation += 1; east_relation += 1
max-visits: 1


@concordat_negotiate
title: Negotiate a Concordat between the Germany and the Vatican.
view-if: prussian_concordat = 1 and reichskonkordat = 0 and reichskonkordat_progress < 4
on-arrival: reichskonkordat_progress += 1; reichskonkordat_progress += 1 if z_leader == "Kaas"; reichskonkordat_progress -= 1 if abortion_rights; reichskonkordat_progress -= 1 if family_law; z_relation += 3*(1-dissent); dvp_relation -= 2; ddp_relation -= 1; ddp_cohesion -= 0.25; foreign_goal_spd_peoples += 1

The Roman Catholic Church is interested in furthering relations with the German Republic, seeing us as preferable to the old anti-Catholic policies of the Empire. We are working towards the signing of a Concordat, an agreement between the Vatican and the Republic that formally recognizes the Church's rights and interests in the country. The <span style="color: #000000;">Center Party</span> is especially interested in making this agreement. The liberal parties are against this idea.

[? if family_law : Our liberalized divorce policies make it difficult to build relations with the Vatican. ?] [? if abortion : Our legalization of abortion make it difficult to connect with the Church. ?] [? if z_leader == "Kaas" : Kaas from the <span style="color: #000000;">Center Party</span> is able to use his connections to speed up negotiations. ?]

@concordat
title: We can sign a Concordat with the Vatican!
view-if: reichskonkordat_progress >= 4 and reichskonkordat = 0
on-arrival: reichskonkordat = 1; z_relation += 15*(1-dissent); catholics_spd += 8*(1-dissent); catholics_z += 5 if in_weimar_coalition or in_grand_coalition or in_popular_front; foreign_goal_spd_peoples += 1
achievement: reichskonkordat

We have successfully signed a Concordat between Germany and the Roman Catholic Church! This treaty defines a formal relationship between the Church and the German state, guaranteeing freedom of worship for Catholics, and the right of the church to appoint their own clergy. Catholics in Germany are pleased, as are their representatives in the <span style="color: #000000;">Center Party</span>.



@european_union
title: Take steps to form a "<span style="color: #001489;">European</span> <span style="color: #FFDD00;">Union</span>".
subtitle: Is a "United States of Europe" possible?
unavailable-subtitle: [? if west_relation < 5 : Our relations with the West are not good enough. ?] [? if east_relation < 4 : Our relations with the East are not good enough. ?]
view-if: reparations <= -2 and eu_progress < 2
choose-if: west_relation >= 5 and east_relation >= 4
on-arrival: eu_progress += 1; west_relation += 1; foreign_goal_spd_peoples += 1

Forming a "<span style="color: #FFDD00;">United States</span> of <span style="color: #001489;">Europe</span>" has long been part of the <span style="color: #c00000;">**Social Democratic**</span> foreign program. Finally, we are taking steps towards it. Given our positive relations with the other countries of <span style="color: #001489;">Europe</span>, we can begin to hold multilateral negotiations for more <span style="color: #001489;">European</span> <span style="color: #FFDD00;">federalism</span>. It will take time to hammer out the exact shape of this future "<span style="color: #001489;">European</span> <span style="color: #FFDD00;">Union</span>", however.


@european_union_2
title: We can sign a treaty to form a <span style="color: #001489;">European</span> <span style="color: #FFDD00;">Union</span>!
view-if: west_relation >= 5 and east_relation >= 4 and eu_progress >= 2
on-arrival: eu = 1; unemployed -= 2 if unemployed >= 10; inflation += 2 if inflation <= -2; inflation -= 1 if inflation >= 4.5; workers_spd += 5*(1-dissent); new_middle_spd += 5*(1-dissent); old_middle_spd += 5*(1-dissent); rural_spd += 4*(1-dissent); catholics_spd += 4*(1-dissent); unemployed_spd += 4*(1-dissent); pro_republic += 5; nationalism -= 5; budget += 1; ddp_left += 3; ddp_cohesion += 3; ddp_relation += 10; foreign_goal_spd_peoples += 1
max-visits: 1
achievement: eu

Along with <span style="color: #002654;">France</span>, <span style="color: #C8102E;">Great Britain</span>, and many other states of Western and Eastern Europe, we have signed a treaty to form a <span style="color: #001489;">European</span> <span style="color: #FFDD00;">Union</span>! It is hoped that <span style="color: #001489;">Europe</span> has learned the lessons of the Great War; never again will there be another war that tears the continent apart.

A major component of the union is the free movement of people and goods across the countries of Europe, as well as the stabilization of currency exchange rates. This provides new markets for German products and new sources of investment.

<span style="color: #7A3C00;">Fascist</span> Italy remains a problem, and the <span style="color: #750e0e;">Soviet Union</span> also lies outside the system. But for now, there is new hope for peace and prosperity throughout the continent.
