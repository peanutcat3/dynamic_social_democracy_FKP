title: The Death of Hermann Müller
view-if: year = 1931 and month >= 3
frequency: 1000
max-visits: 1
tags: event
go-to: pm if chancellor = "Müller"; not_pm if chancellor != "Müller"
on-arrival: muller_dead = 1; n_advisors -= 1 if muller_advisor == 1; muller_advisor = 0;

@pm
on-arrival: muller_died_in_office = 1

Chancellor Hermann Müller has died while in office. He will be greatly missed. But now, we must select a new Chancellor.

- @braun_chancellor: Otto Braun (<span style="color: #c00000;">**SPD**</span>) for Chancellor.
- @wels_chancellor: Otto Wels (<span style="color: #c00000;">**SPD**</span>) for Chancellor.
- @breitscheid_chancellor: Rudolf Breitscheid (<span style="color: #c00000;">**SPD**</span>) for Chancellor.
- @bruning_chancellor: Let Heinrich Bruning of the [? if z_party_name != "CVP": <span style="color: #000000;">Center Party</span>?][? if z_party_name == "CVP": <span style="color: #000000;">**CVP**</span>?] become Chancellor.
- @wirth_chancellor: Joseph Wirth of the [? if z_party_name != "CVP": <span style="color: #000000;">Center Party</span>?][? if z_party_name == "CVP": <span style="color: #000000;">**CVP**</span>?] should be Chancellor.

@braun_chancellor
on-arrival: chancellor = "Braun"; chancellor_party = "SPD"; z_relation -= 3; dvp_relation -= 3; ddp_relation -= 3; lvp_relation -= 3; z_relation -= 3 if prussia_leader == "Braun"; dvp_relation -= 3 if prussia_leader == "Braun"; ddp_relation -= 3 if prussia_leader == "Braun"; lvp_relation -= 3 if prussia_leader == "Braun"
subtitle: The [? if z_party_name != "CVP": <span style="color: #000000;">Center Party</span>?][? if z_party_name == "CVP": <span style="color: #000000;">**CVP**</span>?] is worried about having so much power in one man.

Our coalition partners are reluctant to accept the new Chancellor, but they do not make loud objections.


@wels_chancellor
on-arrival: chancellor = "Wels"; chancellor_party = "SPD"; z_relation -= 3; dvp_relation -= 3; ddp_relation -= 3; lvp_relation -= 3
subtitle: He is a good organizer, but is he also a good chancellor?

Our coalition partners are reluctant to accept the new Chancellor, but they do not make loud objections.

@breitscheid_chancellor
on-arrival: chancellor = "Breitscheid"; chancellor_party = "SPD"; z_relation -= 5; dvp_relation -= 5; ddp_relation -= 5; lvp_relation -= 5; kpd_relation += 3
subtitle: The [? if z_party_name != "CVP": <span style="color: #000000;">Center Party</span>?][? if z_party_name == "CVP": <span style="color: #000000;">**CVP**</span>?] is worried about his left-leaning tendencies.

Our coalition partners are reluctant to accept the new Chancellor, but they do not make loud objections.

@bruning_chancellor
on-arrival: chancellor = "Brüning"; chancellor_party = "Z"; z_relation += 5; dvp_relation += 5; ddp_relation += 5; lvp_relation += 5; coalition_dissent -= 1; dvp_left += 1; ddp_left += 1; ddp_cohesion += 1; lvp_left += 1
subtitle: The [? if z_party_name != "CVP": <span style="color: #000000;">Center Party</span>?][? if z_party_name == "CVP": <span style="color: #000000;">**CVP**</span>?] claims it is their turn...

The [? if z_party_name != "CVP": <span style="color: #000000;">Center Party</span>?][? if z_party_name == "CVP": <span style="color: #000000;">**CVP**</span>?] is grateful that we have supported Brüning for Chancellor.

@wirth_chancellor
on-arrival: chancellor = "Wirth"; chancellor_party = "Z"; z_relation += 5; dvp_relation += 2; ddp_relation += 2; lvp_relation += 2
view-if: z_ideology == "Left"
subtitle: As a moderate member of the [? if z_party_name != "CVP": <span style="color: #000000;">Center Party</span>?][? if z_party_name == "CVP": <span style="color: #000000;">**CVP**</span>?], he could represent unity.

Our coalition partners accept Joseph Wirth of the [? if z_party_name != "CVP": <span style="color: #000000;">Center Party</span>?][? if z_party_name == "CVP": <span style="color: #000000;">**CVP**</span>?] as the new Chancellor.



@not_pm

Hermann Müller, one of the <span style="color: #c00000;">**SPD**</span>'s long-time leaders, has died. He will be greatly missed.
