title: Chancellor [+ chancellor +] replaced by Franz von Papen
subtitle: President Hindenburg dismisses the government.
view-if: (chancellor = "Brüning" or chancellor = "Wirth") and ((year = 1932 and month >= 5) or year >= 1933) and president = "Hindenburg" and spd_in_government = 0 and hindenburg_angry_bruning >= 200 and not presidential_powers
on-arrival: old_chancellor = chancellor; spd_toleration = 0; spd_toleration_right = 0; chancellor = "Papen"; chancellor_party = "I"; next_election_time = time+3 if next_election_time >= time+3; next_election_month = month + 3 if next_election_time >= time+3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; judicial_reform -= 2 if judicial_reform >= 2; finance_minister_party = "I"; interior_minister_party = "I"; economic_minister_party = "I"; labor_minister_party = "I"; foreign_minister_party = "I"; agriculture_minister_party = "I"; reichswehr_minister = "I"; papen_chancellor_timer = 4; sa_strength *= 1.6 if sa_nationwide_ban; sa_militancy += 0.1 if sa_nationwide_ban; sa_nationwide_ban = 0; sa_banned = 0
new-page: true
tags: event
max-visits: 1
face-image: img/portraits/PapenFranz.jpg

= Chancellor [+ old_chancellor +] replaced by Franz von Papen

President Hindenburg's secretive group of advisors (often called the "camarilla") has turned against [+ old_chancellor +], and Hindenburg his dismissed the Chancellor, replacing him with the more <span style="color: #808080;">reactionary</span> Franz von Papen, an independent formerly of the <span style="color: #000000;">Center Party</span>. [? if year == 1932 and old_chancellor == "Brüning": Brüning's final straw was his "agricultural settlement" policy, sending unemployed workers to homestead in underutilized lands (that happen to be owned by aristocrats). ?] [? if strife > 6 : Seeing the chaos that has gripped the country, Hindenburg's advisors seem to believe that the country needs stronger, more authoritarian rulers, and they see Papen as that kind of ruler. ?]

Papen is immediately faced with a vote of no confidence, given his unpopularity. However the <span style="color: #c00000;">**SPD**</span> votes, he is guaranteed to fall, due to the unanimous hatred against him from all other parties. Meanwhile, he is governing via emergency powers with Hindenburg. His first policy is to legalize the <span style="color: #7A3C00;">Nazi</span> <span style="color: #7A3C00;">**SA**</span>, but this does not win their loyalty.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].
