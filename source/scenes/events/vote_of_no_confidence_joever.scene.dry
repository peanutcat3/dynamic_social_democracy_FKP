title: The Right calls a vote of no confidence against the current government!
subtitle: We have angered our coalition partners too much...
new-page: true
on-arrival: tried_supporting_kpd = 0; z_relation -= 5; dvp_relation -= 5; ddp_relation -= 5; lvp_relation -= 5
view-if: spd_in_government = 1 and federal_coalition_trigger == time
tags: event
go-to: vonc_menu

= Vote of No Confidence

Due to the formation of the <span style="color: #000000;">**[+ z_party_name +]**</span>, our coalition is no more! It's joever...

@vonc_menu

- @support_kpd: Could we get the <span style="color: #700000;">**KPD**</span> to abstain?
- @let_it_happen: Let the vote happen.

@support_kpd
choose-if: ((in_grand_coalition and communist_coalition >= 3 and kpd_relation >= 50 and kpd_r >= dvp_r and not lvp_formed) or (in_grand_coalition and communist_coalition >= 3 and kpd_relation >= 50 and kpd_r >= (lvp_r / 2) and lvp_formed)) or (in_weimar_coalition and kpd_relation >= 50 and kpd_r >= z_r and communist_coalition >= 3)
view-if: tried_supporting_kpd = 0
on-arrival: tried_supporting_kpd = 1
unavailable-subtitle: [? if communist_coalition < 3 : The <span style="color: #700000;">KPD</span> won't take part in parliamentary government. ?] [? if kpd_relation < 50 : The <span style="color: #700000;">KPD</span> is not friendly enough with us. ?] [? if ((in_grand_coalition and kpd_r < dvp_r and not lvp_formed) or (in_grand_coalition and kpd_r < (lvp_r / 2) and lvp_formed)) or (in_weimar_coalition and kpd_r < z_r): The <span style="color: #700000;">KPD</span> does not have enough votes. ?] [? if in_popular_front : We are already in a Popular Front with the <span style="color: #700000;">KPD</span>. ?]

If the <span style="color: #700000;">**Communists**</span> abstain, then we essentially can maintain a minority government[? if president=="Hindenburg":, although it would be very difficult to make policy without the support of President Hindenburg, which we do not have ?]. If this happens, then we will own a massive debt to the <span style="color: #700000;">**KPD**</span>. This could lead to broader cooperation in the future, for better or for worse.

- @support_kpd_2: Persuade the <span style="color: #700000;">**KPD**</span> to "tolerate" us?
- @let_it_happen: Never mind. Let the vote happen.

@support_kpd_2
on-arrival: communist_coalition += 1; workers_kpd += 3; kpd_relation += 5
go-to: support_kpd_weimar if in_weimar_coalition; support_kpd_grand if in_grand_coalition


The <span style="color: #700000;">**KPD**</span> is not interested in a "toleration" arrangement; they will only support us if they can participate in the government as part of a coalition. And, if this toleration agreement happens, the right-wing parties will not be inclined to support us. 

@support_kpd_grand
on-arrival: support_kpd_grand = 1

- @election_1928.popular_front_coalition: Form a "Popular Front" coalition?
- @election_1928.left_coalition
- @let_it_happen: Never mind. Let the vote happen.

@support_kpd_weimar
on-arrival: support_kpd_weimar = 1

- @election_1928.left_coalition
- @let_it_happen: Never mind. Let the vote happen.


@let_it_happen
on-arrival: time_to_election = 3; spd_in_government = 0; grand_coalition_collapsed = 1 if in_grand_coalition; in_grand_coalition = 0; in_weimar_coalition = 0; in_popular_front = 0; spd_toleration = 0; spd_toleration_right = 0; vice_chancellor = "Dietrich"; foreign_minister_party = "DVP"; foreign_minister_party = "LVP" if lvp_formed; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = ddp_name; economic_minister_party = "LVP" if lvp_formed; finance_minister_party = "DVP"; finance_minister_party = "LVP" if lvp_formed; agriculture_minister_party = "Z"; labor_minister = "Stegerwald"; interior_minister = "Wirth"; finance_minister = "Moldenhauer"; economic_minister = "Curtius"; justice_minister = "Koch-Weser"; foreign_minister = "Stresemann" if not stresemann_dead; foreign_minister = "Curtius" if stresemann_dead; agriculture_minister = "Dietrich"; reichswehr_minister = "Groener"; coalition_dissent = 0; pro_republic -= 4; old_middle_spd -= 3; new_middle_spd -= 2; workers_spd -= 3 if pro_republic < 50; workers_spd -= 4 if pro_republic < 40; old_middle_nsdap += 4 if pro_republic < 50; new_middle_nsdap += 3 if pro_republic < 50
go-to: let_it_happen_hindenburg if president != "Braun"; let_it_happen_braun if president == "Braun"

@let_it_happen_hindenburg
on-arrival: chancellor = "Brüning"; chancellor_party = "Z"; welfare -= 2; bruning_event_timer = 6 if not bruning_event_seen and year >= 1930
go-to: elections if bruning_event_seen or year < 1930

The vote succeeds, and our government has resigned. Chancellor Brüning has been appointed as the head of a new cabinet, consisting solely of the bourgeois parties. He immediately proceeds to cut welfare spending substantially.

[? if not bruning_event_seen and year >= 1930: New elections haven't been called yet but it's likely that they will be soon, considering Brüning most likely doesn't have the majority of the Reichstag behind him. ?]

@elections
call: set_next_election_time

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@let_it_happen_braun
on-arrival: spd_caretaker = 1
call: set_next_election_time

The vote succeeds, and our government has resigned. Chancellor [+ chancellor +] remains as the head of a caretaker government, but no longer has the power to enact new policies.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].
