title: Hindenburg Sacks [+ chancellor +]!
subtitle: Hindenburg has turned on us...
view-if: (president == "Hindenburg" and not presidential_powers and spd_in_government and hindenburg_angry >= 115 and hindenburg_angry_trigger) and chancellor != "Brüning" and next_election_time - time > 3 and not in_spd_majority
priority: -1
new-page: true
on-arrival: dvp_relation -= 8; ddp_relation -= 4; lvp_relation -= 4; z_relation -= 4 if chancellor_party != "Z"; pro_republic -= 3; pro_republic -= 1 if spd_r >= 30; pro_republic -= 1 if weimar_coalition >= 50; coup_progress -= 1; hindenburg_sacked = 1
go-to: bruning_right if (bruning_event_seen or year < 1930) and right_coalition >= 50 and (dnvp_ideology == "Moderate" or dnvp_westarp) and not (year = 1929 and month >= 7); bruning_center_right if (bruning_event_seen or year < 1930) and (center_right_coalition >= 50 and right_coalition < 50) and dnvp_ideology == "Moderate" and not (year = 1929 and month >= 7); bruning_chain if not ((bruning_event_seen or year < 1930) and right_coalition >= 50 and (dnvp_ideology == "Moderate" or dnvp_westarp) and not (year = 1929 and month >= 7)) and not ((bruning_event_seen or year < 1930) and (center_right_coalition >= 50 and right_coalition < 50) and dnvp_ideology == "Moderate" and not (year = 1929 and month >= 7))
tags: event

= Chancellor [+ chancellor +] Sacked

President Hindenburg has made his dissatisfaction with the <span style="color: #c00000;">socialist</span> presence in government clear and has used his presidential powers to remove us by force. It seems like this decision wasn't a rash one, but rather a matter of finding the appropriate pretext to dismiss our government.

[? if spd_votes >= 31:The <span style="color: #c00000;">SPD</span> remaining electorally resilient despite the radicalizing effects of the economic depression has thwarted Hindenburg’s plan to burden us with governance and erode our popularity. Realizing his strategy has failed, he now moves to dismiss us from government to prevent further <span style="color: #c00000;">socialist</span> reforms from fueling our rising support. ?][? if unemployed >= 15 or economic_growth < 0: Germany's economic crisis and our inadequate response have severely tested Hindenburg's patience, as he favors an austerity-driven approach. ?][? if nsdap_votes >= 18: The rapid rise of the <span style="color: #7A3C00;">far</span>-<span style="color: #3E88B3;">right</span> in local elections has pushed Hindenburg to appoint a government more in line with the national mood. ?][? if rearmament_exposed or reichswehr_loyalty <= 0.1: Kurt von Schleicher and the Reichswehr, outraged by our actions, have leveraged their influence over Hindenburg to push for our removal. ?][? if capital_strike_progress >= 5: Business leaders, frustrated by our policies, have pressured Hindenburg’s inner circle to act against us. ?]

This is a blatant violation of democracy, but we are powerless to stop it. Our only hope is to rally enough support in the next election to reclaim our mandate. 

[? if started: (Dear player, PLEASE don't ragequit, toleration is completely changed from Red Autumn. You still have a chance.) ?]

@bruning_right
on-arrival: spd_in_government = 0; spd_toleration = 0; spd_toleration_right = 0; grand_coalition_collapsed = 1 if in_grand_coalition; chancellor = "Brüning"; in_grand_coalition = 0; in_weimar_coalition = 0; coalition_dissent = 0; chancellor_party = "Z"; foreign_minister_party = "DVP"; foreign_minister_party = "LVP" if lvp_formed; interior_minister_party = "DNVP"; justice_minister_party = "DNVP"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = "DVP"; economic_minister_party = "LVP" if lvp_formed; finance_minister_party = "Z"; agriculture_minister_party = "DNVP"; in_right_coalition = 1; dvp_right += 2; lvp_right += 2; liberal_cooperation -= 1; liberal_cooperation -= 3 if liberal_parliament > 0; liberal_parliament = 0 if liberal_parliament > 0; bourgeois_cooperation += 1

Replacing us is Heinrich Brüning of the [? if z_party_name != "CVP": <span style="color: #000000;">Center Party</span>?][? if z_party_name == "CVP": <span style="color: #000000;">**CVP**</span>?], who has reorganized his cabinet to encompass the right-wing parties, including the <span style="color: #3E88B3;">**DNVP**</span> [? if dnvp_ideology == "Radical": who reluctantly accede to Hindenburg's wishes for a government.?][? if dnvp_ideology == "Moderate":, who are happy to participate in such a coalition, to keep out the <span style="color: #c00000;">socialists</span> from government.?] Their coalition frustratingly holds a majority in the Reichstag, meaning we are forced to accept the loss and prepare for the next elections.

[? if liberal_parliament == 1: The liberal parliamentary group has been broken apart due to the <span style="color: #C0A054;">**DVP**</span> acting on its own accord. ?]

@bruning_center_right
on-arrival: spd_in_government = 0; spd_toleration = 0; spd_toleration_right = 0; grand_coalition_collapsed = 1 if in_grand_coalition; in_grand_coalition = 0; in_weimar_coalition = 0; coalition_dissent = 0; chancellor = "Brüning"; chancellor_party = "Z"; foreign_minister_party = "DVP"; foreign_minister_party = "LVP" if lvp_formed; interior_minister_party = "DNVP"; justice_minister_party = ddp_name; justice_minister_party  = "LVP" if lvp_formed; justice_minister_party = "LVP" if lvp_formed; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = "DVP"; economic_minister_party = "LVP" if lvp_formed; finance_minister_party = "Z"; agriculture_minister_party = "DNVP"; in_center_right_coalition = 1; dvp_right += 1; liberal_cooperation += 1; ddp_cohesion -= 1; ddp_right += 1; lvp_right += 1; bourgeois_cooperation += 2

Replacing us is Heinrich Brüning of the [? if z_party_name != "CVP": <span style="color: #000000;">Center Party</span>?][? if z_party_name == "CVP": <span style="color: #000000;">**CVP**</span>?], who has reorganized his cabinet to encompass the right-wing parties, including the <span style="color: #3E88B3;">**DNVP**</span> who are happy to participate in such a coalition, to keep out the <span style="color: #c00000;">socialists</span> from government. Their coalition frustratingly holds a majority in the Reichstag, meaning we are forced to accept the loss and prepare for the next elections.

@bruning_chain
on-arrival: spd_in_government = 0; minority_government = 1; grand_coalition_collapsed = 1 if in_grand_coalition; in_grand_coalition = 0; in_weimar_coalition = 0; coalition_dissent = 0; chancellor = "Brüning"; chancellor_party = "Z"; foreign_minister_party = "DVP"; foreign_minister_party = "LVP" if lvp_formed; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = ddp_name; economic_minister_party = "LVP" if lvp_formed; finance_minister_party = "DVP"; finance_minister_party = "LVP" if lvp_formed; agriculture_minister_party = "Z"; bruning_event_timer = 6 if not bruning_event_seen and year >= 1930

Replacing us is Brüning of the [? if z_party_name != "CVP": <span style="color: #000000;">Center Party</span>?][? if z_party_name == "CVP": <span style="color: #000000;">**CVP**</span>?], who has reorganized his cabinet to encompass the bourgeois parties. [? if bruning_event_seen or year < 1930: Although they lack a majority in the Reichstag, we could tolerate their government at the next session to prevent new elections, effectively acquiescing to Hindenburg's wishes. ?][? if not bruning_event_seen and year >= 1930: New elections haven't been called yet but it's likely that they will be soon, considering Brüning most likely doesn't have the majority of the Reichstag behind him. ?]

- @tolerate
- @dont_tolerate

@tolerate
title: Oppose any votes of no confidence, voting for the government.
subtitle: We would essentially provide "toleration" to a bourgeois government.
view-if: bruning_event_seen or year < 1930
on-arrival: spd_toleration = 1; left_dissent += 10; pro_republic -= 3; workers_kpd += 3; workers_nsdap += 3; workers_spd -= 3; unemployed_kpd += 5; unemployed_nsdap += 3; unemployed_spd -= 5; dvp_right += 2; ddp_right += 1; lvp_right += 2; liberal_cooperation += 1; welfare -= 2

Brüning immediately begins to cut welfare using his emergency powers granted by President [+ president +]. We try to convince the workers that this policy is necessary in order to maintain a stable parliamentary government, and to deal with the fiscal consequences of the depression. Still, this policy provides excellent recruiting fodder for <span style="color: #7A3C00;">Nazis</span> and <span style="color: #700000;">**Communists**</span>.

@dont_tolerate
title: Support the vote of no confidence, voting against the government.
subtitle: The government will collapse, and there will be new elections.
view-if: bruning_event_seen or year < 1930
on-arrival: time_to_election = 3; spd_in_government = 0; in_grand_coalition = 0; spd_toleration = 0; spd_toleration_right = 0; pro_republic -= 5
call: set_next_election_time

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].