title: Party Disunity
new-page: true
is-card: true
view-if: party_disunity_timer = 0 and (dissent >= 0.3 or (reformist_dissent > 30 and not reformists_resign) or (center_dissent > 30 and not centrists_resign) or (left_dissent > 30 and not sapd_formed) or (labor_dissent > 30 and not unions_independent) or (neorevisionist_dissent > 30 and neorevisionism))
on-arrival: month_actions += 1; party_disunity_timer = 4
card-image: img/Reichstagsfraktion_der_SPD.jpg
frequency: 200
tags: party_affairs


= Party Disunity

The party is being torn apart by internal dissent. Upset at the leadership and general direction, factions have started sabotaging their rivals, hampering our efforts to win votes and carry out our policies.

[? if reformist_dissent > 30 : The reformists are upset at our direction.  ?]
[? if center_dissent > 30 : The centrists are displeased with the direction of our party. ?]
[? if labor_dissent > 30 : The trade unions disagree with our present course of action. ?]
[? if left_dissent > 30: Leftists are making noises about another party split. ?]
[? if neorevisionist_dissent > 30 and neorevisionism = 1 : Neorevisionists are in disagreement about our political direction. ?]

Something must be done to reinforce party unity.

- @enforce_unity
- @concessions_reformists
- @concessions_center
- @concessions_left
- @concessions_labor
- @concessions_neorevisionists
- @do_nothing
- @easy_discard

@enforce_unity
title: Enforce party discipline.
on-arrival: reformist_dissent -= 8; center_dissent -= 8; labor_dissent -= 8; left_dissent -= 8; neorevisionist_dissent -= 8; workers_spd -= (dissent * 10); new_middle_spd -= (dissent * 5); unemployed_spd -= (dissent * 5)

Enforcing party discipline reduces dissent, but it could lead to an exit of dissenting members.

# enforcing party discipline applies to all parties.

# each party has a concessions action and an expulsions action.

@concessions_reformists
title: Give concessions to the reformists.
view-if: reformist_dissent > 30
on-arrival: reformist_dissent -= 15; left_dissent += 10

We have promised to adopt a more reformist posture. This may alienate the left, however.

@concessions_center
title: Give concessions to the centrists.
view-if: center_dissent > 30
on-arrival: center_dissent -= 15; reformist_dissent += 8; left_dissent += 8; neorevisionist_dissent += 8

We have promised to take more policy suggestions from the centrists. Both the left and reformist wings of the party might be displeased.

@concessions_left
title: Give concessions to the left.
view-if: left_dissent > 30
on-arrival: left_dissent -= 15; reformist_dissent += 10; center_dissent += 5

We have promised that we will listen to the left wing of the party. Of course, this leads to increased dissent from the reformists and centrists.

@concessions_labor
title: Give concessions to labor.
view-if: labor_dissent > 30
on-arrival: labor_dissent -= 15; center_dissent += 10

The trade unions will be granted more influence in party decision making. This upsets the centrists, who would prefer to centralize political decision making within the party.

@concessions_neorevisionists
title: Give concessions to the neorevisionists.
view-if: neorevisionist_dissent > 30 and neorevisionism = 1
on-arrival: neorevisionist_dissent -= 15; center_dissent += 10;

Giving the neorevisionists more influence will cause increased dissent from the centrists.


@do_nothing
title: Let the factions argue with themselves.
on-arrival: labor_dissent += 1; left_dissent += 1; center_dissent += 1; reformist_dissent += 1

Let the factions bicker; perhaps dissent is healthy for the party.


@expel_reformists
title: Expel the dissident reformists.
view-if: reformist_dissent > 30

@expel_left
title: Expel the dissident leftists.
view-if: left_dissent > 30

@expel_center
title: Expel the centrists.
view-if: center_dissent > 30

@expel_labor
title: Expel the labor supporters.
view-if: labor_dissent > 30
